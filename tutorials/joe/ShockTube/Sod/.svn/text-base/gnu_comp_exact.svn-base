# GNUPLOT script to plot numerical solution vs. exact solution
# Requires first to set to environment variables: RUN_TYPE
# E.g., export RUN_TYPE="HLLC"

# Screen output 
set terminal x11 persist
set key outside                        # put legend outside 
unset log                              # remove any log-scaling
unset label                            # remove any previous labels
set xtic auto                          # set xtics automatically
set ytic auto                          # set ytics automatically
set xrange [-5:5]
#set yrange [0.0:2e5]
set title "Sod shock tube: `echo $RUN_TYPE`"
set xlabel "$x$"
#set ylabel ""

plot    "<sort -gk 1 ./exact.dat"                        u 1:2 w p lw 1 lt 1 t 'rho ex', \
        "<sort -gk 1 ./shocktube_`echo $RUN_TYPE`.dat"   u 1:2 w l lw 1 lt 1 t 'rho', \
        "<sort -gk 1 ./exact.dat"                        u 1:3 w p lw 1 lt 2 t 'M ex', \
        "<sort -gk 1 ./shocktube_`echo $RUN_TYPE`.dat"   u 1:3 w l lw 1 lt 2 t 'M', \
        "<sort -gk 1 ./exact.dat"                        u 1:4 w p lw 1 lt 3 t 'P ex', \
        "<sort -gk 1 ./shocktube_`echo $RUN_TYPE`.dat"   u 1:4 w l lw 1 lt 3 t 'P ', \
        "<sort -gk 1 ./exact.dat"                        u 1:5 w p lw 1 lt 4 t 'phi ex', \
        "<sort -gk 1 ./shocktube_`echo $RUN_TYPE`.dat"   u 1:6 w l lw 1 lt 4 t 'phi'

# Output for latex
set nokey
set notitle
set terminal epslatex color
set output "shocktube_sod_`echo $RUN_TYPE`.tex"
replot
