# GNUPLOT script to plot numerical solution vs. exact solution
# Requires first to set to environment variables: RUN_TYPE
# E.g., export RUN_TYPE="HLLC"

# Screen output 
set terminal x11 persist
set key outside                        # put legend outside 
unset log                              # remove any log-scaling
unset label                            # remove any previous labels
set xtic auto                          
set ytic border nomirror                         
set y2tic border
set xrange [-1:1]
set yrange [0:2.2]
set y2range [-3:6]
set title "Strong expansion in shock tube: `echo $RUN_TYPE`"
set xlabel "$x$"
set ylabel "$\\rho$, $P$, $T$, $\\phi$"
set y2label "$M$"

plot    "<sort -gk 1 ./shocktube_`echo $RUN_TYPE`.dat"   u 1:2 w l lw 1 lt 1 t 'rho' axes x1y1, \
        "<sort -gk 1 ./shocktube_`echo $RUN_TYPE`.dat"   u 1:4 w l lw 1 lt 3 t 'P' axes x1y1, \
        "<sort -gk 1 ./shocktube_`echo $RUN_TYPE`.dat"   u 1:5 w l lw 1 lt 4 t 'T' axes x1y1, \
        "<sort -gk 1 ./shocktube_`echo $RUN_TYPE`.dat"   u 1:6 w l lw 1 lt 5 t 'phi' axes x1y1, \
        "<sort -gk 1 ./shocktube_`echo $RUN_TYPE`.dat"   u 1:3 w l lw 1 lt 2 t 'M' axes x1y2

# Output for latex
set nokey
set notitle
set terminal epslatex color
set output "shocktube_expansion_`echo $RUN_TYPE`.tex"
replot
